// BloomUpsample.compute
#include "Packages/com.unity.render-pipelines.core/ShaderLibrary/Common.hlsl"
#include "Packages/com.unity.render-pipelines.core/ShaderLibrary/Filtering.hlsl"
#include "Packages/com.unity.render-pipelines.universal/ShaderLibrary/Core.hlsl"
#include "Core.hlsl"

#pragma only_renderers d3d11 playstation xboxone xboxseries vulkan metal switch

#pragma kernel KMain

#pragma multi_compile LOW_QUALITY HIGH_QUALITY

TEXTURE2D_X(_InputLowTexture);
TEXTURE2D_X(_InputHighTexture);

RW_TEXTURE2D_X(float3, _OutputTexture);

SAMPLER(sampler_LinearClamp);

CBUFFER_START(cb0)
    float4 _Params;                 // x: scatter, yzw: unused
    float4 _BloomBicubicParams;     // xy: low src size, zw: low src texel size
    float4 _TexelSize;              // xy; high src size, zw: high src texel size
CBUFFER_END

#define Scatter     _Params.x

#define GROUP_SIZE 8

[numthreads(GROUP_SIZE, GROUP_SIZE, 1)]
void KMain(uint3 dispatchThreadId : SV_DispatchThreadID)
{
    float2 uv = (dispatchThreadId.xy + 0.5) * _TexelSize.zw;
    uv = clamp(uv, 0, 1.0 - _BloomBicubicParams.zw * 0.5);

    uint2 highCoord = clamp(dispatchThreadId.xy, 0, uint2(_TexelSize.xy) - 1);
    float3 highRes = _InputHighTexture[highCoord].xyz;

#if LOW_QUALITY
    float3 lowRes = SAMPLE_TEXTURE2D_X_LOD(_InputLowTexture, sampler_LinearClamp, uv, 0.0).xyz;
#else // HIGH_QUALITY
    float3 lowRes = SampleTexture2DBicubic(
        TEXTURE2D_X_ARGS(_InputLowTexture, 
        sampler_LinearClamp), 
        uv, 
        _BloomBicubicParams, 
        (1.0).xx, 
        unity_StereoEyeIndex
    ).xyz;
#endif

    float3 output = lerp(highRes, lowRes, Scatter);

    // Guard bands
    output *= all(dispatchThreadId.xy <= uint2(_TexelSize.xy));

    _OutputTexture[dispatchThreadId.xy] = output;
}
